# Оголошення персонажів
define i = Character("Я")
define aila = Character("Айла") # Дівчина ельфійка
define mikko = Character("Мікко") # Хлопчик ельф
define anna = Character("Анна") # Дівчина, приблизно однолітки з головним героєм
define andriy = Character("Андрій") # Чоловік середніх років
define sasha = Character("Саша") # Маленький хлопчик
define phone = Character("Телефон")

# Оголошення музичних доріжок
# define audio.idle_music = "audio/Music/Idle.wav"
# define audio.alarm_clock = "audio/Sound/AlarmClock.mp3"

# Оголошення службових змінних
# Змінна лічильника разів підходів до їжі
define eat_counter = 0
# Змінні розмов із людьми
define talk_anna = False
define talk_andriy = False
define talk_sasha = False
# Змінні кінцівок
default persistent.bad_ending = False
default persistent.happy_ending = False

# Оголошення ефекту розплющування та заплющування очей
init python:
    eye_open = ImageDissolve("images/art/eye.png", 0.5, 100)
    eye_close = ImageDissolve("images/art/eye.png", 0.5, 100, reverse=True)
    flash_open = ImageDissolve("images/art/flash_eye.png", 1, 100)
    flash_close = ImageDissolve("images/art/flash_eye.png", 0.75, 100, reverse=True)

label start:
    scene street with fade

    '''
    Чергова співбесіда.{w} Чергова поразка.
    
    Виходжу з офісу, відчуваючи, як надія тане з кожним кроком.
    
    Душна кімната, байдужі погляди, стандартні питання – і передбачуваний результат.

    Хоча кого я обманюю?
    
    Чи міг я справді очікувати чогось іншого?
    
    Ринок праці переповнений, а такі, як я – недосвідчені розробники прямо з коледжу – нікому не потрібні.{w} Навіть задарма.

    Іронія долі – офіс цієї компанії розташований за кілька кварталів від будинку, де ми жили з батьками до мого повноліття.
    
    Ніби саме життя насміхається з моїх амбіцій.

    Зупиняюсь на перехресті.
    
    Праворуч – дорога додому, до порожньої квартири, де мене ніхто не чекає.{w} Ліворуч – вулиці мого дитинства, сповнені спогадів про кращі часи.

    Зітхаю повертаючи ліворуч.
    
    Зрештою, що я втрачаю?
    
    Може, прогулянка місцями, де колись був щасливим, хоч трохи розвіє цю гнітючу атмосферу невдачі.
    '''

    scene street_1 with fade

    '''
    Вулиці дитинства зустрічають мене ностальгічною тишею.
    
    Кожен крок відлунює спогадами, кожен поворот – як машина часу.
    
    Будинки ніби лишились тими ж самими, але водночас стали абсолютно чужими.
    
    Стіни де-не-де облупилися ще більше, де-інде стали охайнішими.
    
    Але вони вже не впізнають мене.
    
    Чи це я не впізнаю себе в їхніх вікнах?

    Школа...{w} Боже, як же я мріяв вирватися з цієї "в'язниці"!
    
    Тепер ця думка викликає гірку посмішку.
    
    Я ж не знав тоді, що це – мої найкращі роки...

    Остання парта – мій особистий острів свободи.
    
    Перешіптування з друзями, записки, жарти – все це створювало відчуття безтурботності, якого мені так не вистачає зараз.
    
    Жодного справжнього напруження, просто сидиш собі і спостерігаєш за життям, що вирує навколо.
    
    І друзі...{w} Де вони зараз?{w} Чи пам'ятають вони мене так само яскраво, як я їх?

    Напевно що ні...

    Як не дивно наша чимала група розпалась як тільки ми отримали атестати.

    Хоча я і досі намагаюсь підтримуємо зв'язок з парою людей звідти, але у них вже давно нове життя, нові інтереси та нові знайомства, я їм ні до чого...

    Як і для Неї...
    
    Тієї самої чий образ – як сон, що розтає на світанку.
    
    Але я досі відчуваю аромат її парфумів – легкий, ніжний, як вранішній вітерець.
    
    А її обійми...{w} Такі теплі й затишні, що я був готовий розтанути в них.
    
    Здається, це було вчора, а минула ціла вічність...

    Після дев'ятого класу я втік у коледж, сповнений надій та амбіцій.
    
    Наївний дурник! Чотири роки промайнули, як один нудний, сірий день.
    
    Онлайн-лекції, самотність перед екраном, жодного справжнього спілкування.
    
    Дистанційне навчання вкрало у мене ці роки, не давши натомість нічого цінного.

    Після чого життя перетворилося на безкінечну гонитву за "саморозвитком" та постійне порівняння себе з іншими в спробах догнати примарного успіху.
    
    І ось я тут – у ямі розчарування, з якої не бачу виходу, окрім самогубства, від якого стримує чито страх, чито те що я досі тримаюсь за старі, нікому не потрібні, абсолютно не помітні досягнення.

    Озираючись на які і думаю: невже всі хороші часи залишилися в минулому?
    
    Чи буде щось краще попереду?
    
    Хотілося б вірити, але серце підказує – навряд чи...
    '''

    scene court with fade

    '''
    За роздумами я й не помітив, як опинився у рідному дворі.
    
    На перший погляд, він особливо не змінився.
    
    Навіть ті ж самі ямки на дорозі не залатали, немов час тут завмер.

    Але щось таки було не так.
    
    Двір відчувався зовсім інакше, ніби чужим.
    
    Може, справа у новому освітленні?
    
    Чи, може, у під'їзди нарешті встановили ті пластикові вікна, про які так довго говорили?

    Намагаючись зрозуміти, що ж змінилося я зробив ще кілька кроків...
    '''

    scene underground with vpunch
    scene black with eye_close

    ""
    "."
    ".."
    "..."

    i "Як же болить нога... Що за чорт?!"

    scene underground with eye_open

    '''
    Різкий біль пронизав все тіло.
    
    Я повільно розплющив очі, намагаючись зрозуміти, що сталося.
    
    Темрява.{w} Холод.{w} Вогкість.
    '''

    i "Де це я, в біса?!"
    i "Невже якийсь недоумкуватий сантехнік забув закрити люк посеред дороги?!"
    
    '''
    Я спробував підвестися, але нога відгукнулася новим спалахом болю.

    Глибоко вдихнувши, я спробував заспокоїтися.
    '''
    
    i "Гаразд, спочатку треба вибратися звідси, а вже потім розбиратися, хто винен."

    '''
    Я озирнувся навколо.
    
    По землі стелився якийсь дивний туман, густий та непроглядний.
    
    Єдине джерело світла – відкритий люк над головою, але його холодне сяйво ледве освітлювало невеликий клаптик простору навколо мене.

    Коли очі звикли до темряви, я помітив щось дивне.
    
    Навколо були якісь руїни, зовсім не схожі на звичайну каналізацію.
    
    Старовинні арки, напівзруйновані колони...
    
    Хіба таке має бути під звичайним міським двором?

    Втім, чесно кажучи, звідки мені знати, як має виглядати справжня каналізація?
    
    Я ж виріс "маминим синочком", навіть на закинуті будівлі ніколи не лазив.
    
    Ця думка викликала гірку посмішку.

    Обережно ступаючи, я пройшов кілька метрів і помітив драбину, що вела нагору.
    
    Вже зібрався вилізти, коли краєм ока вловив щось незвичне – тепле, золотисте світло, що долинало з якогось старовинного коридору праворуч.

    Здоровий глузд благав ігнорувати це і нарешті вибратися звідси.
    
    Але інший, незнайомий голос – голос допитливості, який я ніколи раніше не чув у собі – наполегливо шепотів піти на світло, дослідити коридор.

    Я завмер, вагаючись.
    
    Вперше за довгий час я відчув щось схоже на... азарт?{w} Цікавість?
    
    Що б це не було, воно було сильнішим за страх та біль у нозі.
    '''
    scene tunnel with fade
    '''
    З кожним кроком коридор, подібно тілу змії ставав вужчим.
    
    Холодні стіни, здавалося, наближалися, а стеля опускалася все нижче.
    
    Страх, липкою павутиною обгортав мене з голови до ніг, але якась невідома сила все ще штовхала вперед.

    Я йшов на те неприродно тепле світло, що манило з темряви.
    
    Воно було єдиним орієнтиром у цьому лабіринті темноти та сирості.
    
    Коридор здавався нескінченним, кожен крок відлунював у тиші, посилюючи відчуття самотності та відірваності від реального світу.

    Повітря ставало важчим та душнішим щосекунди.
    
    Я відчував, як піт стікає по спині, а дихання стає уривчастим.
    
    Чи то від страху, чи то від задухи – я вже не міг розрізнити.

    Раптом, немов міраж у пустелі, попереду з'явилися величезні, товсті двері.
    
    Вони були повністю зроблені з дерева, оббиті потемнілим від часу залізом.
    
    Поверхня дверей була вкрита дивними символами – якісь руни чи ієрогліфи, значення яких я не міг навіть уявити.
    '''

    i "Що це за місце?"
    i "Чому тут такі двері?"
    i" Це ж має бути звичайна каналізація, чи не так?"

    '''
    Ці думки билися відлунням у голові, ніби загнаний птах.

    Страх сковував моє тіло.
    
    Ноги наче приросли до землі, відмовляючись рухатися далі.
    
    Кожен інстинкт кричав: "Розвернись! Біжи геть звідси!".
    
    Але світло, що пробивалося з-під дверей, манило з неймовірною силою.

    Що там всередині?
    
    Чому воно тут, глибоко під землею, в місці, яке мало б бути звичайною каналізацією?
    
    Цікавість боролася зі страхом, і, на мій подив, перемагала.

    Я мусив дізнатися.
    
    Ця думка пульсувала в голові, заглушаючи голос розуму.
    
    Зібравши всю свою відвагу, я повільно підійшов до дверей.
    
    Кожен крок давався з неймовірним зусиллям, ніби я йшов проти сильного вітру.

    Серце калатало так сильно, що, здавалося, от-от вискочить з грудей.
    
    Його стукіт відлунював у вухах, заглушаючи всі інші звуки.
    
    Я затамував подих, відчуваючи, як тремтить рука, коли я потягнувся до масивної залізної ручки.

    Секунда вагання – і я рішуче потягнув важкі двері на себе.

    Вони піддалися з неочікуваною легкістю, ніби запрошуючи увійти...
    '''
    scene white with flash_open
    # scene meeting_0 with flash_close
    scene banquet_hall with flash_close
    show mikko at center
    show aila at left
    '''
    Двері розчинилися, і потік теплого світла на мить засліпив мене. Я примружився, чекаючи, поки очі звикнуть до яскравого сяйва.

    Коли зір прояснився, я побачив, що опинився у невеличкій залі чимось схожій на таверну з раніше популярних ігор про середньовіччя.

    Вона була не більше десяти квадратних метрів, але здавалася просторішою завдяки високій стелі.
    
    Світло лилося від старовинної люстри, на якій танцювали вогники кількох свічок, відкидаючи химерні тіні на стіни.

    У дальньому кутку мій погляд привернули дивовижні рослини.
    
    Спочатку я прийняв їх за квіти, але вони були неприродно великими, з ніжними пелюстками та бутонами, що більше нагадували химерні пащі тварин ніж щось, що могло бути у рослини.
    
    Можливо, їхні розміри здавалися перебільшеними через невеликі розміри кімнати, але все одно вони виглядали нереальними.

    Єдиними предметами в залі, окрім цих дивних рослин та люстри, був величезний стіл з темного дерева, що стояв рівно оп центру кімнати.
    
    За ним сиділо кілька осіб різного віку.
    
    Коли я увійшов, вони раптово замовкли та повернулися в мій бік, але вже за мить вони знову до своїх справ.

    Серед присутніх особливо виділялися двоє молодих людей – хлопець і дівчина.
    
    Їхні обличчя були надзвичайно витонченими, з гострими рисами, які здавалися майже нереальними.
    
    Очі сяяли неприродним блиском, а вуха... здавалося були трохи загострені?
    
    Вони нагадували персонажів з фентезійних історій, яких я читав у дитинстві.

    Раптом хлопець з дивною зовнішністю заговорив до мене.
    '''
    # scene meeting_1 with fade
    $ mikko = Character("???")
    $ aila = Character("???")

    scene banquet_hall

    show mikko at center
    show aila at left

    mikko "Вітаю, госте. Прошу, приєднуйся до нашого столу."

    "Я завмер на порозі, вражений неймовірною картиною перед очима."

    i "Дякую за запрошення. Я..."

    mikko "Тут імена не мають значення. Просто насолоджуйся моментом і стравами."

    i "Але я хотів би..."

    mikko "Ні-ні, жодних представлень. Все прийде з часом. Зараз головне - трапеза."

    "На диво, ці слова здалися мені цілком природними та не викликали жодних зайвих думок, ніби я кожен день потрапляю у такого роду ситуації."
    
    "Я підійшов до столу і сів, ніби в трансі."

    mikko "Смакуй, поки гаряче."

    "Думки вирували в голові, але єдине, чого я справді прагнув - це їжа."

    "Я почав накладати собі різноманітні страви з розкішного столу, переповненого наїдками."

    i "Вибачте, але мені все ж цікаво... Як до вас звертатись?"

    mikko "Взагалі, ви можете просто кликати нас власниками, але якщо гостю так кортить використовувати імена, то не будемо заперечувати."

    mikko "Мене звуть Мікко."

    $ mikko = Character("Мікко")

    aila "А я - Айла."

    $ aila = Character("Айла")

    i "Які незвичні імена... Наче з ельфійських легенд."

    "Жартома кинув я."

    "Раптово Айла зупинилася, дивлячись мені просто в очі. Її погляд був дуже здивований."

    "Вони з Міко перезирнулись, після чого її здивування на обличчі змінилося теплою посмішкою."

    aila "Ти майже вгадав."

    "Вона повільно відкрила маленький медальйон на шиї, і я побачив символ, що нагадував ельфійський знак з книжок Толкіна."

    aila "Наш батько був ельфом, а матір людиною, тож ми з братом - напівкровки."

    "Чому її голос здавався мені дуже знайомим."
    
    "Де ж я міг його чути?"

    "Ця думка свердлила мені мозок, але я ніяк не міг згадати чи й же він..."

    "Втім напевно це вже не так і важливо." 
    
    "Все одно ті люди кому він міг належати давно не в моєму житті..."

    i "А інші гості? Як їх звати?"

    mikko "Це ти сам можеш дізнатися."

    aila "Так, поспілкуйся з ними. У кожного тут своя історія..."

    i "Справді?"

    aila "Звісно. До цього місця випадково не потрапляють..."

    hide mikko
    hide aila

    '''
    Ці слова ельфійки викликали дивне відчуття тривоги.

    Я глибоко вдихнув, і воно розчинилося.
    
    Час познайомитися з іншими гостями.

    Навпроти сидів чоловік середніх років, який постійно наповнював і спустошував свій келих.

    На дальньому кінці столу - дівчина з довгим чорним волоссям.{w} Вона щось швидко писала в блокноті, який то ховала, то діставала.

    Біля дверей, крізь які я увійшов, сидів хлопчик років семи.{w} Він весело намагався зачепити ельфів-офіціантів, які снували туди-сюди з тарілками.
    '''

    jump talk_with_everyone
    return

label talk_with_everyone:
    menu:
        "Почати трапезу" if eat_counter == 0:
            if eat_counter == 0:
                "Я обрав невеличку закуску з величезного столу. Перший же шматочок вибухнув феєрверком смаків у роті."
                "Ніби хтось зібрав всі найкращі спеції світу і створив ідеальну гармонію."
                "Боже, якби мама спробувала це, вона б точно випитала рецепт!"
            elif eat_counter == 1:
                "Друга страва виявилася ще смачнішою! Соковите м'ясо тануло на язиці."
                "Я ледве стримався, щоб не застогнати від задоволення. Ельфійська кухня - це щось неймовірне!"
                "Якщо це сон, то я не хочу прокидатися."
            elif eat_counter == 2:
                "Хоч третя порція йшла важко, але я не міг відмовити собі у задоволенні."
                "Десерт виявився справжнім витвором мистецтва - і на вигляд, і на смак."
                "Мабуть, так відчувають себе боги на бенкетах в Олімпі!"
            elif eat_counter == 3:
                "Четверта порція ледь влізла. Я відчував себе як удав, що проковтнув слона."
                "Але ці неймовірні смаки! Варто було терпіти!"
                "Якщо я лусну від переїдання, то хоч помру щасливим."
            else:
                "Я більше не можу їсти! Здається, я перетворився на велетенську кульку."
                "Ще шматочок - і мене доведеться викочувати звідси."
            $ eat_counter += 1
            jump talk_with_everyone
        "Повернутись до трапези" if eat_counter > 0:
            if eat_counter == 1:
                "Я не зміг встояти перед спокусою і повернувся до столу."
                "Цього разу я обрав щось екзотичне - фрукт, якого ніколи раніше не бачив."
                "Смак був неймовірний - суміш ананаса, манго і чогось невідомого, але божественного."
                "Напевно, це те, що Адам і Єва скуштували в Едемському саду!"
            elif eat_counter == 2:
                "Третя порція. Я вже відчував, як мій живіт починає протестувати, але не міг зупинитися."
                "Цього разу - дивовижний суп. Кожна ложка - нова симфонія смаків."
                "Якби існував Оскар за їжу, цей суп отримав би всі нагороди!"
            elif eat_counter == 3:
                "Четверта порція. Мій шлунок благав про пощаду, але розум вимагав ще."
                "Я взяв щось схоже на пиріжок. Перший укус - і я зрозумів, що це найкраще, що я коли-небудь їв."
                "Ось воно, сенс життя - в цьому пиріжку!"
            else:
                "Я більше не можу! Здається, я перетворився на кулю для боулінгу."
                "Якщо мене зараз штовхнути, я покочуся і зіб'ю всі кеглі життя."
            $ eat_counter += 1
            jump talk_with_everyone
        "Поговорити із чоловіком навпроти" if talk_andriy == False:
            $ andriy = Character("???")
            show andriy at center
            i "Добрий вечір. Можна до вас?"
            "Чоловік навіть не підняв очей від келиха, лише махнув рукою на вільний стілець."
            andriy "Валяй..."
            i "Як вас звати?"
            "Він важко зітхнув, ніби саме питання його втомлювало."
            andriy "Андрій. А ти чого хочеш?"
            $ andriy = Character("Андрій")
            i "Просто поговорити. Як ви тут опинилися?"
            "Андрій нарешті подивився на мене. Його очі були втомлені і налиті кров'ю."
            andriy "Слухай, хлопче, я не в настрої для світських бесід. Хочеш щось випити - пий. Хочеш посидіти мовчки - сиди. Але не чіпляйся до мене з дурними питаннями."
            menu:
                "Спробувати підбадьорити":
                    i "Знаєте, іноді розмова з незнайомцем може допомогти. Може, поділитеся, що вас турбує?"
                    "Андрій раптом вибухнув гнівом."
                    andriy "Ти що, чортів психолог?! Я тобі що, піддослідний кролик?!"
                    "Він різко встав, перекинувши келих."
                    andriy "Слухай сюди, розумнику. Ти гадки не маєш, через що я пройшов. Тож засунь свої \"поради\" куди подалі і дай мені спокій!"
                    "З цими словами Андрій відштовхнув стілець і пішов геть, залишивши мене в шоці від його реакції."
                "Залишити в спокої":
                    i "Вибачте, я не хотів вас турбувати. Просто подумав, що вам, можливо, потрібна компанія."
                    "Андрій довго дивився на мене, потім його погляд трохи пом'якшав. Він зробив ще один ковток і важко зітхнув."
                    andriy "Знаєш... Ти перший, хто за довгий час запитав, як у мене справи, не очікуючи нічого натомість."
                    "Він помовчав, ніби вагаючись, чи варто продовжувати."
                    andriy "Я тут, бо втомився. Втомився від життя, яке сам собі створив."
                    "Андрій подивився на свої руки, ніби бачив їх вперше."
                    andriy "Колись я думав, що успіх - це все. Робота, гроші, статус... Я гнався за цим, як божевільний."
                    "Його голос став тихішим, майже шепіт."
                    andriy "Знаєш, що найгірше? Я досяг усього, чого хотів. Велика компанія, мільйонні контракти, розкішний будинок... І що? Виявилося, що все це - пусте."
                    "Він зробив ще один ковток, його очі затуманились від спогадів."
                    andriy "Сьогодні вранці я мав підписати найбільший контракт у своєму житті. Але замість цього я просто... пішов. Пішов у ліс і блукав там годинами, намагаючись зрозуміти, де ж я так схибив."
                    i "Звучить важко. Але ви вже зробили перший крок - усвідомили проблему."
                    "Андрій слабко посміхнувся, вперше за весь час нашої розмови."
                    andriy "Може, ти й маєш рацію. Знаєш, я навіть не пам'ятаю, коли востаннє просто гуляв лісом. Або розмовляв з кимось просто так, не про роботу."
                    "Він замовк на мить, ніби щось обдумуючи."
                    andriy "А потім я спіткнувся, впав... І прокинувся тут. Дивно, але вперше за багато років я відчув... полегшення? Ніби всі ті проблеми, які здавалися такими важливими, раптом стали дрібницями."
                    i "І що ви збираєтеся робити далі?"
                    andriy "Чесно? Не знаю. Вперше в житті я не маю плану. І знаєш що? Це лякає, але водночас... бадьорить."
                    "Андрій подивився на мене з вдячністю."
                    andriy "Дякую, що вислухав, хлопче. Бажаю тобі не наробити тих помилок, що я. Цінуй те, що дійсно важливо - не гроші чи статус, а людей навколо тебе і моменти життя."
                    "Він підняв свій келих, ніби виголошуючи тост."
                    andriy "За нові початки, якими б дивними вони не були."
                    "Оголосивши це він перекинув келих та спустошив його за один ковток."
            "Я відчув, що розмова з Андрієм закінчилася та лишив його наодинці зі своїми думками."
            hide andriy at center
            $ talk_andriy = True
            jump talk_with_everyone

        # ------------------------------------------------------------------------
        # Частина на переробку
        # ------------------------------------------------------------------------

        "Поговорити із хлопчиком біля дверей" if talk_sasha == False:
            $ sasha = Character("???")
            show sasha at center
            i "Привіт! Як тебе звати?"
            sasha "Я Сашко. А ти один із ельфів?"
            $ sasha = Character("Сашко")
            i "Ні-ні, я не один із ельфів, я просто гість, як вони кажуть."
            sasha "Зрозуміло. А розкажеш як ти сюди попав?"
            i "Та я в люк упав, і потім побачив якісь двері, то й зайшов сюди."
            sasha "Так... Щось не дуже цікаво."
            sasha "А я от якось уві сні сюди прийшов!" 
            sasha "Уявляєш собі, прокидаюсь - а тут підземка!"
            i "То ти втік з дому типу?"
            sasha "Ні. Я просто заснув, а прокинувся тут."
            sasha "Але я тут не сумую! Тут я зустрів Анну і ельфів - вони класні!"
            sasha "Ми тепер друзі! Разом їмо смачну їжу за столом!"
            if talk_anna == False:
                i "Вибач, а хто така ця Анна?"
                sasha "Та, он вона сидить. З волоссям догми."
                i "А, зрозумів."
                i "Вибач що перебив. Продовжуй"
                sasha "Та в принципі, я наче все і розказав."
            i "Добре, але як же батьки? Вони ж напевно переживають за тебе. Може тебе додому провести?"
            sasha "Та ні. У мене немає дому. Вихователька сказала, що батьки мене залишили в коробці на вулиці коли я тільки народився."
            i "То ти з дитбудинку?"
            sasha "Ні. Тепер я живу тут. Там не весело. А тут весело!"
            sasha "Ну всьо, я знову хочу їсти. Тож не заважай."
            "Я вже почав відходити від малюка, як він кинув мені навздогін."
            sasha "А, і філе, оце, смажене, не чіпай! Воно моє. Я вже з ельфами домовився!"
            i "Ага. Добре."
            "Я все ж повернувся на своє місце."
            hide sasha at center
            $ talk_sasha = True
            jump talk_with_everyone
        
        
        # "Поговорити із дівчиною з краю столу" if talk_anna == False:
        #     show anna at center
        #     $ anna = Character("???")
        #     if talk_sasha == True:
        #         $ anna = Character("Анна")
        #         i "Вибач, ти ж Анна?"
        #         anna "Так, а ти звідки знаєш?"
        #         i "Та тільки що з Сашком розмовляв, каже що ти класна."
        #         anna "А, той малий. Так підходив щось розказував.. Втім а яка там різниця."
        #         i "Та так..."
        #         "Між нами запанувала тиша."
        #         "Напевно дистанційка все ж давалась взнаки і тут..."
        #         i "Схоже розмова зайшла у глухий кут, тож давай почнемо з початку."
        #     i "Привіт! Як справи?"
        #     anna "Емм, та нічого, все нормально..."
        #     $ anna = Character("Анна")
        #     i "Справді? Якось ти не дуже впевнено сказала."
        #     anna "Ні, зі мною все гаразд, не переймайся."
        #     i "Я бачу, що щось тебе турбує. Можеш розповісти мені, якщо хочеш."
        #     anna "Ні-ні, все добре..."
        #     i "Не виглядає, що все добре. Я готовий вислухати, якщо потрібно виговоритися."
        #     anna "Ну... Якщо ти наполягаєш... Хоча, напевно, тобі буде нецікаво слухати мої проблеми."
        #     i "Абсолютно ні! Розповідай, я з цікавістю послухаю."
        #     anna "Ну гаразд... Тільки обіцяй, що не будеш сміятися..."
        #     i "Обіцяю, розповідай спокійно."
        #     anna "Отож... З дитинства я мріяла стати художницею..."
        #     anna "Закінчила художню школу, академію мистецтв... А тепер вимушена малювати на замовлення, щоб якось вижити."
        #     anna "Я вже втратила натхнення... Мої картини виходять бездушними."
        #     anna "Просто не знаю, що робити далі... Мистецтво більше не приносить мені радості."
        #     i "Розумію... Я теж колись таким був."
        #     anna "Угу"
        #     "Натужно прягла вона. А я не міг вичавити із себе ні якого підбадьорення, нічого."
        #     if talk_sasha == True:
        #         i "До речі. Ти ж тут довше всіх, так?"
        #         anna "А ну так, я сюди перша \"прийшла\""
        #         i "То вже щось цікаве бачила, чи може ельфи щось розповідали?"
        #         anna "Та ні, вони між собою постійно шушукають і тільки і міняють їжу."
        #         i "Зрозуміло. Ну то дякую за інформацію. Може якось віддячу тобі."
        #         "Жартома кинув я."
        #         anna "Та, ні не варто."
        #         anna "І щоб ти знав. Якщо це був спосіб взяти у мене номер, то це було дійсно безглуздо."
        #         i "Зрозумів."
        #     "З рештою я повернувся на своє місце."
        #     hide anna at center
        #     $ talk_anna = True
        #     jump talk_with_everyone
        "Поговорити із дівчиною з краю столу" if talk_anna == False:
            show anna at center
            if talk_sasha == False:
                $ anna = Character("???")
            "Мій погляд зупинився на дівчині, що сиділа осторонь. Її руки невпинно рухалися над блокнотом, ніби намагаючись вхопити думки, що вислизають."
            i "Привіт! Не проти, якщо я присяду?"
            "Вона підняла очі, в яких промайнуло легке роздратування."
            anna "Взагалі-то, я зайнята."
            i "Вибач, я просто хотів познайомитися. Я тут новенький."
            "Дівчина зітхнула, неохоче закриваючи блокнот."
            anna "Ну добре. Що ти хочеш?"
            if talk_sasha == True:
                i "Ти ж Анна, так? Сашко про тебе розповідав."
                $ anna = Character("Анна")
                "Її брови злегка піднялися, а в очах промайнув інтерес."
                anna "А, той малий... Так, це я. І що ж він тобі наговорив?"
                i "В основному, що ти дуже класна."
                anna "Зрозуміло."
                "Трошки зашарівшись сказала вона."
            else:
                i "Я... просто хотів познайомитися. Як тебе звати?"
                "Вона на мить завагалася, ніби зважуючи, чи варто відповідати."
                anna "Анна."
                $ anna = Character("Анна")
            i "Приємно познайомитися, Анно. А що ти пишеш? Виглядає захопливо."
            "Анна напружилася, її погляд став обережним."
            anna "Нічого особливого. Просто думки."
            i "То ти письменниця?"
            "Її очі спалахнули, а голос став холоднішим."
            anna "Слухай, я справді зайнята. Якщо ти просто хочеш поговорити, може, знайдеш когось іншого?"
            if talk_sasha == True:
                i "Вибач, я не хотів тебе турбувати. Просто... Сашко сказав, що ти тут довше за всіх. Може, ти вже щось цікаве бачила, чи ельфи щось розповідали?"
                "Анна трохи розслабилася, але все ще виглядала настороженою."
                anna "Та ні, вони між собою постійно шепочуться і тільки міняють страви. Нічого особливого."
                i "Зрозуміло. Ну, дякую за інформацію. Може, колись віддячу."
                "Жартома кинув я."
                "Анна ледь помітно посміхнулася."
                anna "Не переймайся. І... якщо це був спосіб взяти мій номер, то це було дійсно безглуздо."
                i "Зрозумів. Не буду більше заважати."
            else:
                i "Вибач, я не хотів тебе турбувати. Просто... це місце таке дивне. Я думав, може, ти знаєш щось більше."
                "Анна трохи пом'якшала, але все ще виглядала настороженою."
                anna "Я тут не довше за інших. І знаю не більше за тебе."
                i "Зрозуміло. А як ти сюди потрапила?"
                "Вона на мить задумалася, ніби вагаючись, чи варто відповідати."
                anna "Я... не пам'ятаю. Просто опинилася тут. А ти?"
                i "Впав у люк. Дивно, правда?"
                anna "Все тут дивно."
                "Між нами запанувала тиша. Я відчував, що Анна не налаштована на довгу розмову."
                i "Слухай, а що ти думаєш про господарів?"
                "Анна знизала плечима, але її погляд став серйознішим."
                anna "Вони... незвичайні. Але я б не радила надто багато про них розпитувати."
                i "Чому?"
                anna "Просто відчуття. Краще не лізти, куди не просять."
                i "Зрозуміло. Ну, дякую за розмову. Може, ще побачимось."
                anna "Угу."
                "Вона вже знову відкрила свій блокнот, даючи зрозуміти, що розмова закінчена. Я повільно підвівся, відчуваючи дивну суміш цікавості і тривоги."                
            hide anna at center
            $ talk_anna = True
            jump talk_with_everyone
        "Поговорити із Айлою та Мікко":
            show mikko at left
            show aila at right
            mikko "То, поговорив із усіма?"
            if talk_andriy == True and talk_anna == True and talk_sasha == True:
                i "Так."
                aila "І як тобі? Я була права що буде цікаво?"
                i "Так, наче так. Але вони всі якісь нещасні.{w} Як...{w} Як я..."
                aila "В цьому і є наш секрет. Сюди не можуть попасти щасливі люди. Ми з Мікко створили це місце, щоб допомагати таким, як ти, і як вони..."
                mikko "Чекай-чекай. Далі ще рано розповідати."
                i "Та, ну тобі що шкода?"
                mikko "Ні-ні, просто краще деякі таємниці залишити на потім."
                mikko "А поки залиш все це і просто насолоджуйся."
            else:
                i "Ні. Мені чесно кажучи не було так вже цікаво."
                aila "А я думала, тобі сподобається..."
                aila "Ну добре. Тоді не бери це все дурне до голови та насолоджуйся трапезою."
            i "Ага, дякую."
            "Позіхаючи сказав я."
            jump final_choice
    return

label final_choice:
    scene black with eye_close
    scene banquet_hall with eye_open
    "Я вже щиро кажучи не знаю, скільки часу минуло з того моменту, як я сюди потрапив."
    "Їжа та напої постійно поновлювалася ельфами, проте я відчув, що вже занадто пізно і пора збиратися додому."
    i "Мікко, Айла, дуже дякую за гостинність, але, мабуть, мені вже час."
    show mikko at right
    show aila at left
    mikko "Ні-ні, куди поспішати? Залишайся ще."
    aila "Так, лишайся з нами. Нам так приємно що ти тут разом з нами."
    "На мій подив після цих слів усе моє бажання йти одразу ж зникло, ніби його й не було."
    i "Гаразд, трішки ще побуду."
    aila "Чудово! Ми дуже раді."
    "Усмішка Айли, з якою вона це сказала, зігріла моє серце."
    "Як і тоді, у школі..."
    "Намагаючись відкинути ці думки я проковтнув чергову картоплину, я поглянув на Андрія."
    "Схоже, йому вже справді було досить."
    "Він хитався на стільці, намагаючись щось белькотіти неслухняним язиком."
    i "Ей, Мікко, здається, хлопцеві вже вистачить. Може, викличемо йому таксі?"
    mikko "Та ні не треба, зараз ми з Айлою його вкладемо."
    i "Ну добре."
    "Мікко та Айла підійшли до Андрія ззаду, поплескали його по плечу і щось прошепотіли йому на вухо."
    "Він раптом підвівся і попрямував до кімнати, у бік величезних квітів."
    "Раптом одна квітка розкрилася просто перед ним, наче лев, що роззявляє пащу, і завмерла в очікуванні."
    "Андрій обережно ліг всередину, заплющивши очі."
    "В ту ж мить квітка стулила пащу і знов стала звичайним бутоном."
    i "Що це, до біса, було?!"
    "Вигукнув я з переляку в бік ельфів."
    mikko "Та нічого. Він просто ліг спати."
    "Абсолютно спокійно відповів Мікко."
    "Я аж закипів від побаченого."
    i "Та ж квітка щойно його проковтнула!"
    mikko "Про що ти? Це звичайне ліжко. Чи, може, тобі теж зле?"
    mikko "Хоча, якщо дуже цікаво, можу розповісти тобі таємницю..."
    mikko "Це застілля влаштоване не просто так. Згодом ти все зрозумієш."
    mikko "Коли будеш готовий, ми тебе про дещо запитаємо. А поки що просто розважайся, любий гостю."
    i "Гаразд, зрозумів."
    scene black with eye_close
    scene banquet_hall with eye_open
    "Черговий раз розплющивши очі я помітив, що нікого за столом вже не залишилось."
    "Ні Анни, ні Сашка вже не було, тільки Мікко та Айла крутились довкола столу знову і знову замінюючи страви на столі."
    show mikko at left
    show aila at right
    i "Мікко, Айло, я вже напевно піду. Я якось трохи втомився. Тож до побачення."
    mikko "Стій, гостю. Залишися ще не надовго. У нас ще залишилось кілька цікавих страв для тебе."
    i "Та, ні дякую. Я вже відчуваю, що не зможу більше їсти. Тож я напевно піду."
    aila "Ні-ні. Нащо кудись іти, відпочинь тут. Он остання квітка вже готова прийняти тебе, щоб ти нарешті відпочив."
    i "Велике вам дякую, але я не хочу вас ще якось напружувати, тож я напевно піду. Скільки я маю заплатити за це все?"
    mikko "Жодних грошей тут. Не ображай нас такими речами."
    aila "Залишайся. Хіба ти не хочеш відпочити?"
    aila "Ми ж ельфи, якщо ти не знав уміємо створювати сни, тож зможемо зробити так щоб ти провів уві сні ціле життя з найприємнішими моментами."
    "Вони почали обходити мене з обох боків та заходити за спину."
    aila "Хіба ти не хочеш знову відчути те що відчував тоді?"
    aila "Невже ти не хочеш знову відчути себе щасливим?"
    aila "Невже ти не хочеш щоб Вона відповіла тобі взаємністю?"
    mikko "То що скажеш, любий гостю?"
    menu:
        "Погодитись":
            jump bad_end
        "Відмовитись":
            jump happy_end
    return

# label bad_end:
#     i "Я згоден."
#     aila "От і чудово. Там усе готове."
#     i "Добре, спасибі за турботу."
#     hide mikko
#     hide aila
#     "Не зважаючи на всю ту втому, що накопичилась моєму тілі ноги несли мене до того краю кімнати де величезна квітка вже відкрила пащу аби прийняти мене."
#     "Страху не було."
#     "Я просто йшов уперед у пащу квітки."
#     "Я відчував тільки приємне передчуття, того що я знову відчую те що і раніше, те що так давно втратив."
#     scene black with eye_close
#     "Паща квітки закрилась і я почав відчувати, що втома бере гору."
#     "Я засинаю"
#     "..."
#     "Перед очима починають оживати приємні картини минулого."
#     "Я знову опиняюся у знайомому класі, за своєю звичною партою. Навколо чути неголосне гудіння учнів та скрип крейди по дошці."
#     "Поруч сидять мої старі друзі. Ми перешіптуємось, обмінюємося жартами і тихенько сміємося, намагаючись не привертати увагу вчителя."
#     "Раптом я відчуваю на собі чийсь погляд. Піднімаю очі і бачу Її. Вона посміхається мені тією чарівною посмішкою, яку я пам'ятаю досі."
#     "Ми разом, щасливі. Я нарешті збираюся з силами і кажу їй те, про що давно мріяв - зізнаюся у коханні. І чую у відповідь те саме!"
#     "Неймовірне блаженство огортає все моє єство. Я по-справжньому щасливий!"
#     "Але раптом... десь здалеку чую голоси Мікко та Айли."
#     mikko "Гарний сьогодні улов."
#     aila "І не кажи. Аж чотирьох зловили. Давно такого не було."
#     mikko "Це точно..."
#     aila "Нам того напевно до кінця зими вистачить!"
#     "Я розумію, що сон всього лише приманка для цієї пастки."
#     "Квітка починає заповнюватися липкою рідиною, що стискає мене."
#     "Але я встиг на мить відчути щастя. І цього достатньо."
#     "Я засинаю востаннє."
#     window hide
#     pause
#     if persistent.bad_ending == False:
#         $ renpy.notify("Кінцівку \"Порожній\" здобуто")
#         $ persistent.bad_ending = True
#     return

label bad_end:
    i "Я... згоден."
    aila "От і чудово. Все вже готове."
    i "Дякую за турботу."
    hide mikko
    hide aila
    '''
    Попри втому, що тяжіє над тілом, ноги самі несуть мене до краю кімнати.
    
    Величезна квітка розкриває свою пащу, готова прийняти мене.
    
    Дивно, але страху немає. Жодного.
    
    Я просто йду вперед, у обійми цієї химерної рослини.
    
    Єдине, що відчуваю - солодке передчуття. Ніби от-от повернуся туди, де був по-справжньому щасливим.
    '''
    scene black with eye_close
    '''
    Паща квітки закривається. Втома бере своє.
    
    Я провалююсь у сон...

    ...

    Перед очима оживають картини минулого. Такі яскраві, такі реальні.
    
    Ось я знову в класі, за своєю партою. Навколо гуде вулик голосів, по дошці скрипить крейда.
    
    Поруч - старі друзі. Ми шепочемось, жартуємо, намагаємось не розсміятися надто голосно.
    
    Раптом відчуваю на собі чийсь погляд. Підіймаю очі - і бачу Її.
    
    Вона посміхається. Тією самою посмішкою, що я пам'ятаю досі.
    
    Ми разом. Щасливі. Я нарешті знаходжу в собі сміливість сказати те, про що мріяв роками.
    
    "Я кохаю тебе." І чую у відповідь те саме.
    
    Неймовірне відчуття блаженства огортає мене. Я справді щасливий!

    Але... Щось не так. Здалеку долинають голоси.
    '''
    mikko "Гарний сьогодні улов."
    aila "І не кажи. Аж чотирьох зловили. Давно такого не було."
    mikko "Це точно..."
    aila "Нам того, мабуть, до кінця зими вистачить!"
    '''
    Що... що відбувається? Сон розсіюється, як туман на сонці. 

    Ні... ні! Я не хочу прокидатися! Благаю, дайте мені ще хвилинку з нею!

    Але реальність невблаганна. Це все обман. Пастка. І я... я потрапив у неї.

    Щось холодне і липке обволікає моє тіло. Не можу поворухнутися. Не можу дихати.

    Паніка накриває мене хвилею. Але... чому я не борюся? Чому не намагаюся втекти?

    Бо... я все ще відчуваю відлуння того щастя. Того моменту, коли вона посміхнулася мені.

    Може, це і є справжнє життя? Ці короткі миті радості серед океану болю?

    Темрява згущується. Думки уповільнюються. 

    Я відчуваю, як свідомість згасає. Це кінець. Але... я не боюся.

    Бо в останню мить я знову був щасливим. І, можливо... можливо, цього достатньо.
    '''
    window hide
    pause
    if persistent.bad_ending == False:
        $ renpy.notify("Кінцівку \"Порожній\" здобуто")
        $ persistent.bad_ending = True
    return

label happy_end:
    i "Вибачте, але я все ж відмовлюсь."
    aila "Що? Ти не можеш відмовитись! Ти маєш залишитись тут, із нами!"
    i "Я кажу ні!"
    i "Я не хочу лишитись на вічно у минулому!"
    i "Я хочу жити далі!"
    "Я різко ривком кинувся до виходу, намагаючись якомога швидше втекти звідси."
    hide mikko
    hide aila
    "Але ноги наче налилися свинцем, кожен крок давався вкрай важко."
    "Тіло ніби розпухло від усієї їжі, яку я похапцем закидав в себе за столом. Але зупинятися я не міг!"
    mikko "Схопіть його!"
    "М'язи не слухалися, але я продовжував рухатися вперед, віддаляючись від переслідувачів."
    scene tunnel with fade
    "Нарешті я добіг до коридору і щодуху кинувся туди."
    scene underground with fade
    "Опинившись у каналізаційному відсіку, я миттєво схопився за драбину."
    "Туман внизу почав розсіюватися, і я якомога швидше поліз нагору, на поверхню."
    "Ледь чутно знизу долинув розчарований вигук Мікко:"
    mikko "От лихо, втік! Ну нічого, трьох нам вистачить."
    scene court with fade
    "Зрештою я вибрався на вулицю та щодуху припустив геть звідти."
    "Озирнувшись, я не побачив ні сліду ельфів, ні люка за собою."
    "Але від гріха подалі я вирішив не зупинятися, а якомога швидше віддалитися."
    scene black with eye_close
    scene court with eye_open
    "Нарешті я доставив ці прокляті документи в офіс! Думав, вони їх ніколи не приймуть."
    "Тепер я можу спокійно повертатися додому."
    "Але раз я вже тут, то пройдуся трохи знайомими місцями - до іншої зупинки."
    "Я минув двір, де колись ледь не потрапив у пастку. Тепер тут все спокійно."
    "Невдовзі попереду з'явилася автобусна зупинка. Я пришвидшив крок."
    init python:
        import os
        user_name = os.environ.get("USERNAME")
    phone "Привіт, [user_name], ти вже скоро будеш?"
    i "Так, вже майже на зупинці, кохана."
    phone "Чудово, чекаю! А, і не забудь купити цукерок для моїх батьків."
    i "Звісно, обов'язково куплю. Скоро побачимося!"
    "Я усміхнувся, згадавши нещодавні події. Якби я тоді погодився залишитися у минулому, то ніколи б не дізнався, яке прекрасне життя мене чекає."
    scene street with fade
    "Тому тепер я дивлюся лише вперед і живу сьогоденням."
    "З цими думками я сів у свій автобус, що віз мене назустріч майбутньому, а не минулому."
    window hide
    pause
    if persistent.happy_ending == False:
        $ renpy.notify("Кінцівку \"Новий шлях\" здобуто")
        $ persistent.happy_ending = True
    return