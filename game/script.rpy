# Оголошення персонажів
define i = Character("Я")
define aila = Character("Айла") # Дівчина ельфійка
define mikko = Character("Мікко") # Хлопчик ельф
define anna = Character("Анна") # Дівчина, приблизно однолітки з головним героєм
define andriy = Character("Андрій") # Чоловік середніх років
define sasha = Character("Саша") # Маленький хлопчик
define phone = Character("Телефон")

# Оголошення музичних доріжок
# define audio.idle_music = "audio/Music/Idle.wav"
# define audio.alarm_clock = "audio/Sound/AlarmClock.mp3"

# Оголошення службових змінних
# Змінна лічильника разів підходів до їжі
define eat_counter = 0
# Змінні розмов із людьми
define is_talk_anna = False
define is_talk_andriy = False
define is_talk_sasha = False
# Змінні щасливості людей
define is_anna_happy = False
define is_andriy_happy = False
define is_sasha_happy = False
# Змінна лічильника спійманих ельфами людей
define caught_count = 1
# Змінні кінцівок
default persistent.bad_ending = False
default persistent.happy_ending = False
# Змінна статусу гри
define persistent.game_status = 0

# Оголошення ефекту розплющування та заплющування очей
init python:
    eye_open = ImageDissolve("images/art/eye.png", 0.5, 100)
    eye_close = ImageDissolve("images/art/eye.png", 0.5, 100, reverse=True)
    flash_open = ImageDissolve("images/art/flash_eye.png", 1, 100)
    flash_close = ImageDissolve("images/art/flash_eye.png", 0.75, 100, reverse=True)

# Екран завантаження
label splashscreen:
    scene black
    with Pause(1)
    show text "Prod. by Nikita Berezhnyj" with dissolve
    with Pause(2)
    hide text with dissolve
    with Pause(1)
    return

# Початок гри
label start:
    $ persistent.game_status = 0
    scene street with fade
    '''
    Чергова співбесіда.{w} Черговий провал.
    
    Виходжу з офісу, відчуваючи, як надія тане з кожним кроком.
    
    Душна кімната, байдужі погляди, стандартні питання – і передбачуваний результат.

    Хоча кого я обманюю?
    
    Чи міг я справді очікувати чогось іншого?
    
    Ринок праці переповнений, а такі, як я – недосвідчені розробники прямо з коледжу – нікому не потрібні.{w} Навіть задарма.

    Іронія долі – офіс цієї компанії розташований за кілька кварталів від будинку, де ми жили з батьками до мого повноліття.
    
    Ніби саме життя насміхається з моїх амбіцій.

    Зупиняюсь на перехресті.
    
    Праворуч – дорога додому, до порожньої квартири, де мене ніхто не чекає.{w} Ліворуч – вулиці мого дитинства, сповнені спогадів про кращі часи.

    Зітхаю повертаючи ліворуч.
    
    Зрештою, що я втрачаю?
    
    Може, прогулянка місцями, де колись був щасливим, хоч трохи розвіє цю гнітючу атмосферу невдачі.
    '''

    scene street_1 with fade

    '''
    Вулиці дитинства зустрічають мене ностальгічною тишею.
    
    Кожен крок відлунює спогадами, кожен поворот – як машина часу.
    
    Будинки ніби лишились тими ж самими, але водночас стали абсолютно чужими.
    
    Стіни де-не-де облупилися ще більше, де-інде стали охайнішими.
    
    Але вони вже не впізнають мене.
    
    Чи це я не впізнаю себе в їхніх вікнах?

    Школа...{w} Боже, як же я мріяв вирватися з цієї "в'язниці"!
    
    Тепер ця думка викликає гірку посмішку.
    
    Я ж не знав тоді, що це – мої найкращі роки...

    Остання парта – мій особистий острів свободи.
    
    Перешіптування з друзями, записки, жарти – все це створювало відчуття безтурботності, якого мені так не вистачає зараз.
    
    Жодного справжнього напруження, просто сидиш собі і спостерігаєш за життям, що вирує навколо.
    
    І друзі...{w} Де вони зараз?{w} Чи пам'ятають вони мене так само яскраво, як я їх?

    Напевно що ні...

    Як не дивно наша чимала група розпалась як тільки ми отримали атестати.

    Хоча я і досі намагаюсь підтримуємо зв'язок з парою людей звідти, але у них вже давно нове життя, нові інтереси та нові знайомства, я їм ні до чого...

    Як і для Неї...
    
    Тієї самої чий образ – як сон, що розтає на світанку.
    
    Але я досі відчуваю аромат її парфумів – легкий, ніжний, як вранішній вітерець.
    
    А її обійми...{w} Такі теплі й затишні, що я був готовий розтанути в них.
    
    Здається, це було вчора, а минула ціла вічність...

    Після дев'ятого класу я втік у коледж, сповнений надій та амбіцій.
    
    Наївний дурник! Чотири роки промайнули, як один нудний, сірий день.
    
    Онлайн-лекції, самотність перед екраном, жодного справжнього спілкування.
    
    Дистанційне навчання вкрало у мене ці роки, не давши натомість нічого цінного.

    Після чого життя перетворилося на безкінечну гонитву за "саморозвитком" та постійне порівняння себе з іншими в спробах догнати примарного успіху.
    
    І ось я тут – у ямі розчарування, з якої не бачу виходу, окрім самогубства, від якого стримує чито страх, чито те що я досі тримаюсь за старі, нікому не потрібні, абсолютно не помітні досягнення.

    Озираючись на які і думаю: невже всі хороші часи залишилися в минулому?
    
    Чи буде щось краще попереду?
    
    Хотілося б вірити, але серце підказує – навряд чи...
    '''

    scene court with fade

    '''
    За роздумами я й не помітив, як опинився у рідному дворі.
    
    На перший погляд, він особливо не змінився.
    
    Навіть ті ж самі ямки на дорозі не залатали, немов час тут завмер.

    Але щось таки було не так.
    
    Двір відчувався зовсім інакше, ніби чужим.
    
    Може, справа у новому освітленні?
    
    Чи, може, у під'їзди нарешті встановили ті пластикові вікна, про які так довго говорили?

    Намагаючись зрозуміти, що ж змінилося я зробив ще кілька кроків...
    '''

    scene underground with vpunch
    scene black with eye_close

    ""
    "."
    ".."
    "..."

    i "Як же болить нога... Що за чорт?!"

    scene underground with eye_open

    '''
    Різкий біль пронизав все тіло.
    
    Я повільно розплющив очі, намагаючись зрозуміти, що сталося.
    
    Темрява.{w} Холод.{w} Вогкість.
    '''

    i "Де це я, в біса?!"
    i "Невже якийсь недоумкуватий сантехнік забув закрити люк посеред дороги?!"
    
    '''
    Я спробував підвестися, але нога відгукнулася новим спалахом болю.

    Глибоко вдихнувши, я спробував заспокоїтися.
    '''
    
    i "Гаразд, спочатку треба вибратися звідси, а вже потім розбиратися, хто винен."

    '''
    Я озирнувся навколо.
    
    По землі стелився якийсь дивний туман, густий та непроглядний.
    
    Єдине джерело світла – відкритий люк над головою, але його холодне сяйво ледве освітлювало невеликий клаптик простору навколо мене.

    Коли очі звикли до темряви, я помітив щось дивне.
    
    Навколо були якісь руїни, зовсім не схожі на звичайну каналізацію.
    
    Старовинні арки, напівзруйновані колони...
    
    Хіба таке має бути під звичайним міським двором?

    Втім, чесно кажучи, звідки мені знати, як має виглядати справжня каналізація?
    
    Я ж виріс "маминим синочком", навіть на закинуті будівлі ніколи не лазив.
    
    Ця думка викликала гірку посмішку.

    Обережно ступаючи, я пройшов кілька метрів і помітив драбину, що вела нагору.
    
    Вже зібрався вилізти, коли краєм ока вловив щось незвичне – тепле, золотисте світло, що долинало з якогось старовинного коридору праворуч.

    Здоровий глузд благав ігнорувати це і нарешті вибратися звідси.
    
    Але інший, незнайомий голос – голос допитливості, який я ніколи раніше не чув у собі – наполегливо шепотів піти на світло, дослідити коридор.

    Я завмер, вагаючись.
    
    Вперше за довгий час я відчув щось схоже на... азарт?{w} Цікавість?
    
    Що б це не було, воно було сильнішим за страх та біль у нозі.
    '''
    scene tunnel with fade
    '''
    З кожним кроком коридор, подібно тілу змії ставав вужчим.
    
    Холодні стіни, здавалося, наближалися, а стеля опускалася все нижче.
    
    Страх, липкою павутиною обгортав мене з голови до ніг, але якась невідома сила все ще штовхала вперед.

    Я йшов на те неприродно тепле світло, що манило з темряви.
    
    Воно було єдиним орієнтиром у цьому лабіринті темноти та сирості.
    
    Коридор здавався нескінченним, кожен крок відлунював у тиші, посилюючи відчуття самотності та відірваності від реального світу.

    Повітря ставало важчим та душнішим щосекунди.
    
    Я відчував, як піт стікає по спині, а дихання стає уривчастим.
    
    Чи то від страху, чи то від задухи – я вже не міг розрізнити.

    Раптом, немов міраж у пустелі, попереду з'явилися величезні, товсті двері.
    
    Вони були повністю зроблені з дерева, оббиті потемнілим від часу залізом.
    
    Поверхня дверей була вкрита дивними символами – якісь руни чи ієрогліфи, значення яких я не міг навіть уявити.
    '''

    i "Що це за місце?"
    i "Чому тут такі двері?"
    i "Це ж має бути звичайна каналізація, чи не так?"

    '''
    Ці думки билися відлунням у голові, ніби загнаний птах.

    Страх сковував моє тіло.
    
    Ноги наче приросли до землі, відмовляючись рухатися далі.
    
    Кожен інстинкт кричав: "Розвернись! Біжи геть звідси!".
    
    Але світло, що пробивалося з-під дверей, манило з неймовірною силою.

    Що там всередині?
    
    Чому воно тут, глибоко під землею, в місці, яке мало б бути звичайною каналізацією?
    
    Цікавість боролася зі страхом, і, на мій подив, перемагала.

    Я мусив дізнатися.
    
    Ця думка пульсувала в голові, заглушаючи голос розуму.
    
    Зібравши всю свою відвагу, я повільно підійшов до дверей.
    
    Кожен крок давався з неймовірним зусиллям, ніби я йшов проти сильного вітру.

    Серце калатало так сильно, що, здавалося, от-от вискочить з грудей.
    
    Його стукіт відлунював у вухах, заглушаючи всі інші звуки.
    
    Я затамував подих, відчуваючи, як тремтить рука, коли я потягнувся до масивної залізної ручки.

    Секунда вагання – і я рішуче потягнув важкі двері на себе.

    Вони піддалися з неочікуваною легкістю, ніби запрошуючи увійти...
    '''
    scene white with flash_open
    # scene meeting_0 with flash_close
    scene banquet_hall with flash_close
    show mikko at center
    show aila at left
    '''
    Двері розчинилися, і потік теплого світла на мить засліпив мене. Я примружився, чекаючи, поки очі звикнуть до яскравого сяйва.

    Коли зір прояснився, я побачив, що опинився у невеличкій залі чимось схожій на таверну з раніше популярних ігор про середньовіччя.

    Вона була не більше десяти квадратних метрів, але здавалася просторішою завдяки високій стелі.
    
    Світло лилося від старовинної люстри, на якій танцювали вогники кількох свічок, відкидаючи химерні тіні на стіни.

    У дальньому кутку мій погляд привернули дивовижні рослини.
    
    Спочатку я прийняв їх за квіти, але вони були неприродно великими, з ніжними пелюстками та бутонами, що більше нагадували химерні пащі тварин ніж щось, що могло бути у рослини.
    
    Можливо, їхні розміри здавалися перебільшеними через невеликі розміри кімнати, але все одно вони виглядали нереальними.

    Єдиними предметами в залі, окрім цих дивних рослин та люстри, був величезний стіл з темного дерева, що стояв рівно оп центру кімнати.
    
    За ним сиділо кілька осіб різного віку.
    
    Коли я увійшов, вони раптово замовкли та повернулися в мій бік, але вже за мить вони знову до своїх справ.

    Серед присутніх особливо виділялися двоє молодих людей – хлопець і дівчина.
    
    Їхні обличчя були надзвичайно витонченими, з гострими рисами, які здавалися майже нереальними.
    
    Очі сяяли неприродним блиском, а вуха... здавалося були трохи загострені?
    
    Вони нагадували персонажів з фентезійних історій, яких я читав у дитинстві.

    Раптом хлопець з дивною зовнішністю заговорив до мене.
    '''
    # scene meeting_1 with fade
    $ mikko = Character("???")
    $ aila = Character("???")

    scene banquet_hall

    show mikko at center
    show aila at left

    mikko "Вітаю, госте. Прошу, приєднуйся до нашого столу."

    "Я завмер на порозі, вражений неймовірною картиною перед очима."

    i "Дякую за запрошення. Я..."

    mikko "Тут імена не мають значення. Просто насолоджуйся моментом і стравами."

    i "Але я хотів би..."

    mikko "Ні-ні, жодних представлень. Все прийде з часом. Зараз головне - трапеза."

    "На диво, ці слова здалися мені цілком природними та не викликали жодних зайвих думок, ніби я кожен день потрапляю у такого роду ситуації."
    
    "Я підійшов до столу і сів, ніби в трансі."

    mikko "Смакуй, поки гаряче."

    "Думки вирували в голові, але єдине, чого я справді прагнув - це їжа."

    "Я почав накладати собі різноманітні страви з розкішного столу, переповненого наїдками."

    i "Вибачте, але мені все ж цікаво... Як до вас звертатись?"

    mikko "Взагалі, ви можете просто кликати нас власниками, але якщо гостю так кортить використовувати імена, то не будемо заперечувати."

    mikko "Мене звуть Мікко."

    $ mikko = Character("Мікко")

    aila "А я - Айла."

    $ aila = Character("Айла")

    i "Які незвичні імена... Наче з ельфійських легенд."

    "Жартома кинув я."

    "Раптово Айла зупинилася, дивлячись мені просто в очі. Її погляд був дуже здивований."

    "Вони з Міко перезирнулись, після чого її здивування на обличчі змінилося теплою посмішкою."

    aila "Ти майже вгадав."

    "Вона повільно відкрила маленький медальйон на шиї, і я побачив символ, що нагадував ельфійський знак з книжок Толкіна."

    aila "Наш батько був ельфом, а матір людиною, тож ми з братом - напівкровки."

    "Чому її голос здавався мені дуже знайомим."
    
    "Де ж я міг його чути?"

    "Ця думка свердлила мені мозок, але я ніяк не міг згадати чи й же він..."

    "Втім напевно це вже не так і важливо." 
    
    "Все одно ті люди кому він міг належати давно не в моєму житті..."

    i "А інші гості? Як їх звати?"

    mikko "Це ти сам можеш дізнатися."

    aila "Так, поспілкуйся з ними. У кожного тут своя історія..."

    i "Справді?"

    aila "Звісно. До цього місця випадково не потрапляють..."

    hide mikko
    hide aila

    '''
    Ці слова ельфійки викликали дивне відчуття тривоги.

    Я глибоко вдихнув, і воно розчинилося.

    Навпроти сидів чоловік середніх років, який постійно наповнював і спустошував свій келих.

    На дальньому кінці столу - дівчина з довгим чорним волоссям.{w} Вона щось швидко писала в блокноті, який то ховала, то діставала.

    Біля дверей, крізь які я увійшов, сидів хлопчик років семи.{w} Він весело намагався зачепити ельфів-офіціантів, які снували туди-сюди з тарілками.

    Я відчув, як напружуюсь. Зазвичай я уникаю спілкування з незнайомцями, але тут...{w} Щось підказувало мені, що це не просто випадкова компанія.

    Кожен з них, здавалося, мав свою історію, яка могла бути ключем до розуміння цього дивного місця.

    Я завагався. Частина мене хотіла просто сидіти тихо, спостерігаючи, але інша...{w} Інша нашіптувала, що це, можливо, єдиний шанс дізнатися правду.

    "Ти ж хотів змін у своєму житті", - пронеслося у голові. - "Хіба це не достатньо незвичайна ситуація, щоб вийти зі своєї зони комфорту?"

    Я зітхнув, розуміючи, що мій внутрішній голос має рацію. Якщо я хочу зрозуміти, що відбувається, доведеться поговорити з кимось.

    Тепер залишалося лише вирішити, з кого почати...
    '''

    jump talk_with_everyone
    return

# Мапа з вибором з ким поговорити
label talk_with_everyone:
    call screen mapScreen
        
# Трапеза
label feast:
    if eat_counter == 0:
        "Я обрав невеличку закуску з величезного столу. Перший же шматочок вибухнув феєрверком смаків у роті."
        "Ніби хтось зібрав всі найкращі спеції світу і створив ідеальну гармонію."
        "Боже, якби мама спробувала це, вона б точно випитала рецепт!"
    elif eat_counter == 1:
        "Я не зміг встояти перед спокусою і повернувся до столу."
        "Друга страва виявилася ще смачнішою! Соковите м'ясо тануло на язиці."
        "Напевно, це те, що Адам і Єва скуштували в Едемському саду!"
    elif eat_counter == 2:
        "Третя порція. Я вже відчував, як мій живіт починає протестувати, але не міг зупинитися."
        "Десерт виявився справжнім витвором мистецтва - і на вигляд, і на смак."
        "Мабуть, так відчувають себе боги на бенкетах в Олімпі!"
    elif eat_counter == 3:
        "Четверта порція ледь влізла. Я відчував себе як удав, що проковтнув слона."
        "Перший укус - і я зрозумів, що це найкраще, що я коли-небудь їв."
        "Якщо я лусну від переїдання, то хоч помру щасливим."
    else:
        "Я більше не можу їсти! Здається, я перетворився на велетенську кульку."
        "Якщо мене зараз штовхнути, я покочуся і зіб'ю всі кеглі життя."
    $ eat_counter += 1
    jump talk_with_everyone
    return

# Поговорити з Андрієм
label talk_with_andriy:
    $ andriy = Character("???")
    show andriy at center
    '''
    Я повільно підійшов до чоловіка, який невідривно дивився у свій келих.

    Його похмурий вигляд і напружена поза змусили мене завагатися. 
    
    "Можливо, не варто його турбувати?" - промайнула думка. 
    
    Але щось всередині підштовхувало мене зробити цей крок.
    
    Можливо, це була цікавість, а можливо я просто бачив у ньому себе.

    Втім це не так вже й важливо..

    Глибоко вдихнувши, я зібрав всю свою рішучість.
    '''
    i "Добрий вечір. Чи можна приєднатись?"
    "Чоловік навіть не підняв очей від келиха, лише махнув рукою на вільний стілець."
    andriy "Валяй..."
    i "Як вас звати?"
    "Він важко зітхнув, ніби саме питання його втомлювало."
    andriy "Андрій. А ти чого хочеш?"
    $ andriy = Character("Андрій")
    i "Просто поговорити. Як ви тут опинилися?"
    "Андрій нарешті подивився на мене. Його очі були втомлені і налиті кров'ю."
    andriy "Слухай, хлопче, я не в настрої для світських бесід. Хочеш щось випити - пий. Хочеш посидіти мовчки - сиди. Але не чіпляйся до мене з дурними питаннями."
    menu:
        "Спробувати підбадьорити":
            i "Знаєте, іноді розмова з незнайомцем може допомогти. Може, поділитеся, що вас турбує?"
            "Андрій раптом вибухнув гнівом."
            andriy "Ти що, чортів психолог?! Я тобі що, піддослідний кролик?!"
            "Він різко встав, перекинувши келих."
            andriy "Слухай сюди, розумнику. Ти гадки не маєш, через що я пройшов. Тож засунь свої \"поради\" куди подалі і дай мені спокій!"
            "З цими словами Андрій відштовхнув стілець і пішов геть, залишивши мене в щоці від його реакції."
            $ is_andriy_happy = False
        "Залишити в спокої":
            i "Вибачте, я не хотів вас турбувати. Просто подумав, що вам, можливо, потрібна компанія."
            "Андрій довго дивився на мене, потім його погляд трохи пом'якшав. Він зробив ще один ковток і важко зітхнув."
            andriy "Знаєш... Ти перший, хто за довгий час запитав, як у мене справи, не очікуючи нічого натомість."
            "Він помовчав, ніби вагаючись, чи варто продовжувати."
            andriy "Я тут, бо втомився. Втомився від життя, яке сам собі створив."
            "Андрій подивився на свої руки, ніби бачив їх вперше."
            andriy "Колись я думав, що успіх - це все. Робота, гроші, статус... Я гнався за цим, як божевільний."
            "Його голос став тихішим, майже шепіт."
            andriy "Знаєш, що найгірше? Я досяг усього, чого хотів. Велика компанія, мільйонні контракти, розкішний будинок... І що? Виявилося, що все це - пусте."
            "Він зробив ще один ковток, його очі затуманились від спогадів."
            andriy "Сьогодні вранці я мав підписати найбільший контракт у своєму житті. Але замість цього я просто... пішов. Пішов у ліс і блукав там годинами, намагаючись зрозуміти, де ж я так схибив."
            i "Звучить важко. Але ви вже зробили перший крок - усвідомили проблему."
            "Андрій слабко посміхнувся, вперше за весь час нашої розмови."
            andriy "Може, ти й маєш рацію. Знаєш, я навіть не пам'ятаю, коли востаннє просто гуляв лісом. Або розмовляв з кимось просто так, не про роботу."
            "Він замовк на мить, ніби щось обдумуючи."
            andriy "А потім я спіткнувся, впав... І прокинувся тут. Дивно, але вперше за багато років я відчув... полегшення? Ніби всі ті проблеми, які здавалися такими важливими, раптом стали дрібницями."
            i "І що ви збираєтеся робити далі?"
            andriy "Чесно? Не знаю. Вперше в житті я не маю плану. І знаєш що? Це лякає, але водночас... бадьорить."
            "Андрій подивився на мене з вдячністю."
            andriy "Дякую, що вислухав, хлопче. Бажаю тобі не наробити тих помилок, що я. Цінуй те, що дійсно важливо - не гроші чи статус, а людей навколо тебе і моменти життя."
            "Він підняв свій келих, ніби виголошуючи тост."
            andriy "За нові початки, якими б дивними вони не були."
            "Оголосивши це він перекинув келих та спустошив його за один ковток."
            $ is_andriy_happy = True
    "Я відчув, що розмова з Андрієм закінчилася та лишив його наодинці зі своїми думками."
    hide andriy at center
    $ is_talk_andriy = True
    jump talk_with_everyone
    return

# Поговорити з Сашком
label talk_with_sasha:
    $ sasha = Character("???")
    show sasha at center
    '''
    Маленький хлопчик виглядав таким безтурботним і щасливим. 
    
    Я відчув укол заздрості - коли я востаннє був таким? 
    
    "Що я можу сказати дитині?" - занепокоєно подумав я. 
    
    Але його відкрита усмішка трохи заспокоїла мої нерви.
    '''
    sasha "Дядьку, дядьку! Ви теж ельф?"
    i "Ти тут один?"
    i "Ні, я просто... гість, напевно. А ти?"
    sasha "Я Сашко! Приємно познайомитись!"
    $ sasha = Character("Сашко")
    i "То, ти тут один?"
    sasha "Звісно ні. Я тут з ельфами!"
    i "І як ти тут опинився, Сашко?"
    sasha "О, це цікава історія! Я посварився з вихователькою в дитсадочку. Вона сказала, що я неслухняний і що тому в мене ніхто не любить."
    sasha "І потім я заліз у дупло старого дуба в парку, щоб сховатися. І уяви собі - опинився тут!"
    i "Серйозно? Просто так?"
    sasha "Ну майже! Спочатку було темно і страшно, але потім я зустрів ельфів. Вони такі кльові! І годують смачно!"
    i "А ти не хочеш повернутися? Може, хтось хвилюється за тебе?"
    sasha "Та ну їх! Тут набагато веселіше. І ніхто не каже, що я поганий."
    i "Розумію. Але ж не можна просто так зникнути..."
    sasha "Можна! Дивись - я вже зник!"
    hide sasha with dissolve
    "Малий заліз під стіл. Але дуже весело продовжив."
    sasha"І мені добре. Тут є Анна, вона як старша сестра. А от той дядько біля він якийсь старий і страшний, тож я його не люблю. А от ельфи - вони як сім'я."
    if not is_talk_anna:
        i "Анна? Хто вона?"
        sasha "Он та дівчина з довгим волоссям. Вона класна!"
        i "А, бачу. Дякую."
    show sasha at center
    "Раптом Сашко знову з'явився."
    sasha "Слухай, ти ж можеш зберігати секрети? Хочеш мій секрет?"
    menu:
        "Звісно, давай":
            i "Звісно, давай. Я вмію зберігати секрети."
            sasha "Ельфи сказали, що готують найсмачніші млинці з полуницею у світі! Я вже випросив собі порцію. Хочеш спробувати?"
            i "Дякую, звісно, але..."
            sasha "Та я жартую! Воно все моє! Ха-ха!"
            hide sasha with dissolve
            "Сашко весело засміявся і побіг до столу з їжею."
            "Хм, може, йому й справді краще тут..."
            $ is_sasha_happy = True
        "Краще не треба":
            i "Знаєш, Сашко, краще не треба. Секрети іноді можуть бути небезпечними."
            sasha "Ой, ну ти нудний! Я думав, ти крутий, а ти як та вихователька!"
            "Сашко надувся і відвернувся від мене."
            "Можливо, я був занадто суворим з ним..."
            $ is_sasha_happy = False
    hide sasha with dissolve
    $ is_talk_sasha = True
    jump talk_with_everyone     
    return

# Поговорити з Анною
label talk_with_anna:
    show anna at center
    $ anna = Character("???")
    '''
    Дівчина здавалася заглибленою у свої думки, постійно щось записуючи. 

    Як давно я спілкувався із дівчиною свого віку?

    Подумавши про це я реально почав прокручувати всі випадки подумки, після чого дійшов до жахаючого висновку.

    Невже ні разу з випуску зі школи?

    Я відчув, як мої долоні спітніли від хвилювання. 
    
    "А що як я їй заважатиму?" - промайнула думка.
    
    Але цікавість перемогла страх, і я зробив крок вперед.
    '''
    if is_talk_sasha == True:
        $ anna = Character("Анна")
        i "Вибач, ти ж Анна?"
        anna "Так, а ти звідки знаєш?"
        i "Та тільки що з Сашком розмовляв, каже що ти класна."
        anna "А, той малий. Так підходив щось розказував.. Втім а яка там різниця."
        i "Та так..."
        "Між нами запанувала тиша."
        "Напевно дистанційка все ж давалась взнаки і тут..."
        i "Схоже розмова зайшла у глухий кут, тож давай почнемо з початку."
    i "Привіт! Як справи?"
    anna "Емм, та нічого, все нормально..."
    $ anna = Character("Анна")
    i "Справді? Якось ти не дуже впевнено сказала."
    anna "Ні, зі мною все гаразд, не переймайся."
    i "Я бачу, що щось тебе турбує. Можеш розповісти мені, якщо хочеш."
    anna "Ні-ні, все добре..."
    i "Не виглядає, що все добре. Я готовий вислухати, якщо потрібно виговоритися."
    anna "Ну... Якщо ти наполягаєш... Хоча, напевно, тобі буде нецікаво слухати мої проблеми."
    i "Абсолютно ні! Розповідай, я з цікавістю послухаю."
    anna "Ну гаразд... Тільки обіцяй, що не будеш сміятися..."
    i "Обіцяю, розповідай спокійно."
    anna "Отож... З дитинства я мріяла стати художницею..."
    anna "Закінчила художню школу, академію мистецтв... А тепер вимушена малювати на замовлення, щоб якось вижити."
    anna "Я вже втратила натхнення... Мої картини виходять бездушними."
    anna "Просто не знаю, що робити далі... Мистецтво більше не приносить мені радості."
    menu:
        "Спробувати підбадьорити":
            i "Знаєш, Анно, іноді треба зробити крок назад, щоб побачити повну картину. Можливо, тобі варто спробувати нові стилі або техніки? Або малювати те, що приносить тобі радість, а не те, що замовляють?"
            anna "Хм... Я не думала про це в такому ключі. Можливо, ти маєш рацію. Дякую за пораду, це дійсно змушує мене замислитись."
            "Анна слабко посміхнулась, і здається, в її очах з'явився проблиск надії."
            $ is_anna_happy = True
        "Погодитися, що ситуація складна":
            i "Так, розумію тебе. Життя художника дійсно може бути складним. Можливо, варто подумати про іншу професію?"
            anna "Що? Я не можу просто так відмовитися від своєї мрії! Я думала, ти зрозумієш..."
            "Анна відвернулася, явно засмучена моїми словами."
            $ is_anna_happy = False
    if is_talk_sasha == True:
        i "До речі. Ти ж тут довше всіх, так?"
        anna "А ну так, я сюди перша \"прийшла\""
        i "То вже щось цікаве бачила, чи може ельфи щось розповідали?"
        anna "Та ні, вони між собою постійно шушукають і тільки і міняють їжу."
        i "Зрозуміло. Ну то дякую за інформацію. Може якось віддячу тобі."
        "Жартома кинув я."
        anna "Та, ні не варто."
        anna "І щоб ти знав. Якщо це був спосіб взяти у мене номер, то це було дійсно безглуздо."
        i "Зрозумів."
    "З рештою я повернувся на своє місце."
    hide anna at center
    $ is_talk_anna = True
    jump talk_with_everyone
    return

# Поговорити з Ельфами
label talk_with_elf:
    show mikko at left
    show aila at right
    mikko "То, поговорив із усіма?"
    if is_talk_andriy == True and is_talk_anna == True and is_talk_sasha == True:
        i "Так."
        aila "І як тобі? Я була права що буде цікаво?"
        i "Так, наче так. Але вони всі якісь нещасні.{w} Як...{w} Як я..."
        aila "В цьому і є секрет. Сюди не можуть попасти щасливі люди. Ми з Мікко створили це місце, щоб допомагати таким, як ти, і як вони. Таким, хто втратив віру в життя і кого більше не чекають..."
        mikko "Чекай-чекай. Далі ще рано розповідати."
        i "Та, ну тобі що шкода?"
        mikko "Ні-ні, просто краще деякі таємниці залишити на потім."
        mikko "А поки залиш все це і просто насолоджуйся."
    else:
        i "Ні. Мені чесно кажучи не було так вже цікаво."
        aila "А я думала, тобі сподобається..."
        aila "Ну добре. Тоді не бери це все дурне до голови та насолоджуйся трапезою."
    i "Ага, дякую."
    "Позіхаючи сказав я."
    jump final_choice
    return

# Фінальний вибір гри
label final_choice:
    scene black with eye_close
    scene banquet_hall with eye_open
    "Я вже щиро кажучи не знаю, скільки часу минуло з того моменту, як я сюди потрапив."
    "Їжа та напої постійно поновлювалася ельфами, проте я відчув, що вже занадто пізно і пора збиратися додому."
    i "Мікко, Айла, дуже дякую за гостинність, але, мабуть, мені вже час."
    show mikko at right
    show aila at left
    mikko "Ні-ні, куди поспішати? Залишайся ще."
    aila "Так, лишайся з нами. Нам так приємно що ти тут разом з нами."
    "На мій подив після цих слів усе моє бажання йти одразу ж зникло, ніби його й не було."
    i "Гаразд, трішки ще побуду."
    aila "Чудово! Ми дуже раді."
    "Усмішка Айли, з якою вона це сказала, зігріла моє серце."
    "Як і тоді, у школі..."
    "Я намагаюсь знову закинути ці думки куди по далі."
    "До речі, дивно... Чому я так легко погодився залишитись? Щось тут не так."
    "Я озирнувся навколо, намагаючись зрозуміти, що саме мене непокоїть."
    "Ця зала, ці ельфи, ця нескінченна їжа... Все здається одночасно і реальним, і якимось... казковим?"
    "Може, я просто втомився і мені все це здається? Чи, можливо, я потрапив у якусь магічну пастку?"
    "Голова почала трохи паморочитися. Я відчув, як реальність навколо мене ніби трохи пливе."
    "Треба зібратися. Щось тут не так, але що саме? І головне - як мені вибратися звідси?"
    "Хоча, чи варто мені взагалі кудись іти звідси?"
    "Тут до мене ставляться як до людини, годують і тут наче немає жодних проблем.."
    "Ще й голос цієї ельфійки... Він такий знайомий.. Чуючи його я ніби відчуваю, я мене м'яко поглинають спогади і я не можу встояти.."
    "Стій. Стій, я знову загубив свої думки.."
    "Я глибоко вдихнув, намагаючись зосередитися. Відчуття тривоги наростало, але я намагався не подавати виду."    
    "Я знову глянув на Мікко та Айлу, намагаючись зрозуміти, чи помічають вони мій стан."
    i "Треба бути обережним. Можливо, вони не такі доброзичливі, якими здаються. Але як мені діяти далі?"
    "Намагаючись відкинути ці думки я проковтнув чергову картоплину, та глянув на інших гостей."

    # Якщо не поговорив взагалі ні з ким
    if not is_talk_andriy and not is_talk_sasha and not is_talk_anna:
        "Втім вони були мені зовсім не цікаві..."
        "Я оглянув зал, відчуваючи дивну відстороненість від усього, що відбувається навколо."
        "Люди за столом були для мене просто незнайомцями. Їхні розмови, сміх, навіть сльози - все це здавалося таким далеким і неважливим."
        "Чоловік, що сидів неподалік, щось бурмотів собі під ніс, але я не міг розібрати слів. Та й навіщо?"
        "Дитина в кутку плакала, але її сльози не викликали у мене жодних емоцій. Просто ще один звук у загальному гаморі."
        "Дівчина з блокнотом щось малювала, але її творчість мене абсолютно не цікавила."
        "Я відчув, як мої повіки важчають. Ця атмосфера, ці незнайомі обличчя - все це почало зливатися в одну розмиту картину."
        "Можливо, варто просто заснути? Принаймні, уві сні я не буду відчувати цієї дивної порожнечі всередині."
        "Я заплющив очі, дозволяючи собі поринути в сон, байдужий до того, що відбувається навколо."
    # Якщо поговорив хоч із кимось
    else:
        "Чоловік навпроти був явно вже п'яний."
        "Схоже, йому вже справді було досить."
        "Він хитався на стільці, намагаючись щось белькотіти неслухняним язиком."
        "Хлопчак бігав всією таверною та намагався розвеселити дівчину з краю столу, яка не змінила ні, пози ні виразу обличчя від тоді як я потрапив сюди."
        # Перевірка чи говорив із Андрієм
        if is_talk_andriy:
            # Перевірка на радість Андрія
            if is_andriy_happy:
                "Андрій повільно підвівся, його очі світилися новознайденим спокоєм."
                andriy "Знаєте, я багато думав про те, що ви сказали. Про цінність моментів і людей навколо нас."
                "Він обвів поглядом усіх присутніх, ніби бачив їх вперше."
                andriy "Я зрозумів, що моє життя - це не лише робота і гроші. Є щось більше, щось важливіше."
                "Андрій підійшов до Мікко та Айли, які здивовано переглянулися."
                andriy "Дякую вам за гостинність, але мені час повертатися. У мене є незакінчені справи в реальному світі."
                mikko "Але ж, Андрію, хіба тобі тут не добре? Ти можеш залишитися, забути про всі проблеми..."
                "Андрій м'яко, але впевнено похитав головою."
                andriy "Ні, дякую. Я зрозумів, що втеча - це не вихід. Мені потрібно виправити свої помилки, побудувати справжні стосунки з людьми."
                aila "Але ж ми можемо дати тобі все, що ти хочеш! Навіщо повертатися туди, де тобі було так важко?"
                andriy "Бо там - справжнє життя. З усіма його труднощами і радощами. І я хочу прожити його повністю."
                "З цими словами Андрій рішуче попрямував до виходу. Ельфи намагалися його зупинити, але якась невидима сила не дозволяла їм це зробити."
                "Перед тим, як зникнути за дверима, Андрій обернувся і промовив:"
                andriy "Дякую вам усім. Ви допомогли мені зрозуміти, що найважливіше."
                "І з цими словами він вийшов, залишивши після себе відчуття надії та нових можливостей."
                "Я спостерігав, як Андрій йде, і відчув дивне змішання емоцій. Полегшення за нього, але й легкий укол заздрості. Він знайшов свій шлях, а я... Я все ще тут."
            else:
                "Андрій продовжував сидіти, втупившись у свій келих. Його погляд став ще більш похмурим."
                andriy "Ще... Налийте мені ще..."
                "Мікко підійшов до нього з новим келихом."
                mikko "Звісно, друже. Ось, випий і забудь про всі свої турботи."
                "Андрій жадібно схопив келих і осушив його одним ковтком."
                andriy "Знаєте... Ви праві. Навіщо мені повертатися? Там нічого хорошого... Тільки робота, стрес, самотність..."
                "Його очі почали закриватися, а голова хилилася на стіл."
                aila "Так, Андрію. Тут тобі буде краще. Ми подбаємо про тебе."
                "Андрій пробурмотів щось незрозуміле і повністю відключився."
                "Мікко та Айла переглянулися з задоволеними усмішками."
                mikko "Ну от, ще один знайшов свій спокій у нас."
                "Вони акуратно підняли Андрія і понесли його до великої квітки, яка розкрилася, готова прийняти нового гостя."
                "Щось всередині мене здригнулося, коли я побачив, як Андрія понесли до квітки. Чому мені здається, що це неправильно?"
        else:
            "Чоловік середнього віку, який сидів за столом, втупившись у свій келих. Його погляд був похмурим і відстороненим."
            "Мікко та Айла підійшли до нього, пропонуючи ще випити. Чоловік жадібно схопив новий келих і осушив його одним ковтком."
            "За кілька хвилин його очі почали закриватися, а голова хилилася на стіл. Ельфи переглянулися з задоволеними усмішками."
            "Вони акуратно підняли чоловіка і понесли його до великої квітки, яка розкрилася, готова прийняти нового гостя."
            "Щось всередині мене здригнулося, коли я побачив цю сцену. Чому мені здається, що це неправильно? Але я швидко відігнав цю думку."
        
        "Час ніби сповільнився. Звуки бенкету почали затихати, а кольори навколо мене стали трохи тьмянішими. Я відчув, як мої повіки важчають."
        "Голова почала паморочитися. Чи то від випитого вина, чи то від дивного диму, що стелився по підлозі. Намагаючись зосередитися на тому, що відбувається навколо у мене нічого не вийшло."

        scene black with dissolve
        scene banquet_hall with dissolve

        "Звук голосів повернув мене до свідомості. Я розплющив очі, намагаючись зорієнтуватися в ситуації."

        # Перевірка чи говорив із Сашком
        if is_talk_sasha:
            # Перевірка на радість Сашка
            if is_sasha_happy:
                "Раптом Сашко підскочив зі свого місця, його очі сяяли від захвату."
                sasha "Ого! Я згадав! Мама ж обіцяла мені сьогодні почитати нову книжку про пригоди!"
                "Він підбіг до Мікко та Айли, стрибаючи від нетерпіння."
                sasha "Дядьку Мікко, тітонько Айло! Мені треба додому! Я не можу пропустити казку на ніч!"
                "Ельфи виглядали здивованими і трохи засмученими."
                mikko "Але ж, Сашко, хіба тобі тут не подобається? У нас є багато цікавих історій..."
                sasha "Подобається! Але мама... Вона напевно хвилюється. І я скучив за нею."
                "Айла спробувала переконати хлопчика."
                aila "Любий, а як щодо тих млинців з полуницею, які ти так хотів?"
                "Сашко на мить завагався, але потім рішуче похитав головою."
                sasha "Ні, дякую! Мамині млинці - найсмачніші у світі! І я хочу їх спробувати знову!"
                "З цими словами Сашко попрямував до виходу. Ельфи намагалися його зупинити, але якась магія не дозволяла їм цього зробити."
                sasha "Дякую за веселощі! Але мені пора додому. Бувайте!"
                "І з цими словами Сашко вибіг з кімнати, залишивши після себе відлуння дитячого сміху."
                "Дитячий сміх Сашка ще довго відлунював у залі після його відходу. Я замислився про те, як легко діти можуть знаходити радість навіть у найдивніших ситуаціях. Може, нам, дорослим, варто вчитися у них?"
            else:
                "Сашко сидів у кутку, обхопивши коліна руками. Його очі були червоними від сліз."
                sasha "Я хочу додому... Де моя мама?"
                "Мікко підійшов до хлопчика, тримаючи в руках велику тарілку з солодощами."
                mikko "Ну-ну, Сашко. Подивися, які смачні цукерки я тобі приніс!"
                "Сашко підняв очі, але в них не було колишнього блиску."
                sasha "Я не хочу цукерок. Я хочу до мами..."
                "Айла присіла поруч з хлопчиком, ніжно погладжуючи його по голові."
                aila "Любий, тут твій новий дім. Тут ти будеш щасливий, обіцяю."
                "Сашко схлипнув, але вже не мав сил сперечатися."
                sasha "Правда? А мама... Вона знайде мене?"
                mikko "Звісно, малий. А поки що, давай-но я покажу тобі чарівну кімнату, де ти зможеш відпочити."
                "Мікко взяв Сашка за руку і повів його до великої квітки, яка повільно розкрилася."
                "Хлопчик, втомлений від сліз, не чинив опору, коли його клали всередину квітки."
                aila "Солодких снів, Сашко. Тут тобі буде добре."
                "Квітка закрилася, огорнувши хлопчика своїми пелюстками."
                "Серце стиснулося, коли я побачив сльози на обличчі хлопчика. Невже це місце не таке вже й чарівне, як здається на перший погляд?"
        else:
            "У кутку я помітив маленького хлопчика, який сидів, обхопивши коліна руками. Його очі були червоними від сліз."
            "Ельфи підійшли до нього, пропонуючи солодощі та іграшки, але хлопчик лише похитав головою."
            "Зрештою, один з ельфів взяв дитину за руку і повів до великої квітки, яка повільно розкрилася."
            "Хлопчик не чинив опору, коли його клали всередину квітки. Вона закрилася, огорнувши його своїми пелюстками."
            "Я відвернувся, відчуваючи легкий дискомфорт. Але чому це мене так турбує? Зрештою, це не моя справа."

        "В котре за вечір мої думки почали плутатися. Здавалося, що час то прискорювався, то сповільнювався. Це насторожувало, і в той же час, чомусь, заспокоювало. Я заплющив очі, намагаючись зібратися з думками."

        scene black with dissolve
        scene banquet_hall with dissolve

        "Раптовий шурхіт привернув мою увагу. Я помітив рух збоку і повернув голову, щоб побачити джерело звуку."

        # Перевірка чи говорив із Анною
        if is_talk_anna:
            # Перевірка на радість Анни
            if is_anna_happy:
                "Анна раптом граційно випросталася, її очі загорілися натхненням."
                anna "Я... Я знаю, що мені потрібно малювати!"
                "Вона почала гарячково гортати свій блокнот, шукаючи чисту сторінку."
                anna "Це місце, ці відчуття... Вони відкрили мені очі на те, що я насправді хочу створювати!"
                "Мікко та Айла підійшли до неї, зацікавлено спостерігаючи."
                mikko "Але ж, Анно, ти можеш залишитися тут і малювати скільки завгодно. У нас є все необхідне..."
                "Анна похитала головою, не відриваючись від свого блокнота."
                anna "Ні, ви не розумієте. Мені потрібно повернутися. Там, у реальному світі, я зможу втілити ці ідеї, поділитися ними з іншими."
                aila "Але ж тут ти можеш створювати будь-які світи, які тільки забажаєш!"
                "Анна нарешті підняла очі, в них світилася рішучість."
                anna "Саме так. Але справжнє мистецтво народжується з реальних емоцій, з життєвого досвіду. Мені потрібно жити, щоб творити."
                "З цими словами вона закрила блокнот і рішуче попрямувала до виходу."
                anna "Дякую вам за все. Ви допомогли мені зрозуміти, чого я насправді хочу."
                "І перш ніж ельфи встигли щось сказати, Анна вийшла, залишивши після себе відчуття невловимого натхнення."
                "Спостерігаючи за тим, як Анна поспішно збирає свої речі, я відчув дивне поєднання радості за неї і смутку. Вона знайшла своє натхнення, свій шлях. А я? Що тримає мене тут?"
            else:
                "Анна сиділа, втупившись у свій порожній блокнот. Її очі були порожніми, а рука, що тримала олівець, безвільно впала."
                anna "Я... Я більше не можу малювати. Нічого не виходить..."
                "На мить здалося, що вона була повністю спустошена, її думки заплуталися у безвиході. Але раптом щось змінилося в її очах. Вони заблищали сльозами, і Анна, наче блискавка, вхопила блокнот обома руками."
                anna "Нічого не виходить! Чому?! Чому я така бездарна?!"
                "Її голос розлетівся луною по кімнаті, коли вона почала несамовито розривати сторінки блокнота. Олівець впав на підлогу, а сльози, змішані зі злістю, текли по її обличчю."
                "Анна продовжувала рвати блокнот, доки він майже не розпався на шматки. Її рухи ставали все хаотичнішими, а плач - усе голоснішим."
                anna "Ніщо... Ніщо не має сенсу! Все марно!"
                "Врешті-решт, сили покинули її, і вона впустила рештки блокнота на підлогу. Анна впала на коліна, безсило обійнявши себе, і вибухнула в безутішних сльозах."
                "В цей момент Айла підійшла до неї, тримаючи в руках чашку з ароматним напоєм."
                aila "Анно, люба, будь ласка, заспокойся... Випий це, воно допоможе."
                "Мікко підійшов з іншого боку і обережно обійняв Анну за плечі, намагаючись її заспокоїти."
                mikko "Ти не одна, ми тут з тобою. Все буде добре, обіцяю."
                "Анна, все ще здригаючись від ридань, прийняла чашку з рук Айли. Вона зробила кілька ковтків, намагаючись заспокоїтись."
                anna "Я... я не можу більше... Я просто хочу зникнути..."
                "Мікко та Айла обмінялися тривожними поглядами. Вони знали, що Анна перебуває на межі, і що їй потрібно дати відпочити."
                aila "Тут ти зможеш відпочити від усіх турбот. Забути про критику, про невдачі..."
                "Очі Анни почали закриватися, а тіло розслаблятися."
                anna "Так... Відпочити... Це було б чудово..."
                "Мікко та Айла обережно підняли Анну і понесли її до великої квітки."
                aila "Ось так, люба. Тут тобі буде спокійно і затишно."
                "Квітка повільно закрилася, огортаючи Анну своїми м'якими пелюстками."
                mikko "Сподіваюся, у своїх снах вона знайде те натхнення, якого так прагнула."
                "Порожній погляд Анни, коли її вели до квітки, викликав у мене незрозумілу тривогу."
        else:
            "Мою увагу привернула дівчина, яка сиділа за столом з порожнім блокнотом. Її погляд був розгубленим і спустошеним."
            "Раптом вона почала несамовито рвати сторінки блокнота, викрикуючи щось про свою бездарність."
            "Ельфи швидко підійшли до неї, пропонуючи якийсь напій. Дівчина, все ще здригаючись від ридань, але все ж прийняла прийняла чашку."
            "За кілька хвилин її очі почали закриватися, а тіло розслаблятися. Ельфи обережно підняли її і понесли до великої квітки."
            "Я спостерігав за цією сценою з дивним відчуттям. Це виглядало неправильно, але чому мене це так хвилює? Зрештою, я навіть не знаю цю дівчину."
        
        "Мої думки ставали все повільнішими, важчими. Кольори навколо мене почали зливатися, а звуки затихати. Я відчував, як поринаю в якусь дивну дрімоту."
    
    scene black with eye_close
    scene banquet_hall with eye_open
    "Я прокинувся від тиші, що вже деякий час панувала в залі. Голова була важкою, думки плуталися."
    "Всі вже давно залишили залу, і залишився тільки я. Втім Мікко та Айла невпинно крутились довкола столу знову і знову замінюючи страви на ньому."
    show mikko at left
    show aila at right
    i "Мікко, Айло, я вже напевно піду. Я якось трохи втомився. Тож до побачення."
    mikko "Стій, гостю. Залишися ще не надовго. У нас ще залишилось кілька цікавих страв для тебе."
    i "Та, ні дякую. Я вже відчуваю, що не зможу більше їсти. Тож я напевно піду."
    aila "Ні-ні. Нащо кудись іти, відпочинь тут. Он остання квітка вже готова прийняти тебе, щоб ти нарешті відпочив."
    i "Велике вам дякую, але я не хочу вас ще якось напружувати, тож я напевно піду. Скільки я маю заплатити за це все?"
    mikko "Жодних грошей тут. Не ображай нас такими речами."
    aila "Залишайся. Хіба ти не хочеш відпочити?"
    aila "Ми ж ельфи, якщо ти не знав уміємо створювати сни, тож зможемо зробити так щоб ти провів уві сні ціле життя з найприємнішими моментами."
    "Вони почали обходити мене з обох боків та заходити за спину."
    aila "Хіба ти не хочеш знову відчути те що відчував тоді?"
    aila "Невже ти не хочеш знову відчути себе щасливим?"
    aila "Невже ти не хочеш щоб Вона відповіла тобі взаємністю?"
    mikko "То що скажеш, любий гостю?"
    menu:
        "Погодитись":
            jump bad_end
        "Відмовитись":
            jump happy_end
    return

# Погана кінцівка
label bad_end:
    i "Я... згоден."
    aila "От і чудово. Все вже готове."
    i "Дякую за турботу."
    hide mikko
    hide aila
    '''
    Попри втому, що тяжіє над тілом, ноги самі несуть мене до краю кімнати.
    
    Величезна квітка розкриває свою пащу, готова прийняти мене.
    
    Дивно, але страху немає. Жодного.
    
    Я просто йду вперед, у обійми цієї химерної рослини.
    
    Єдине, що відчуваю - солодке передчуття. Ніби от-от повернуся туди, де був по-справжньому щасливим.
    '''
    scene black with eye_close
    '''
    Паща квітки закривається. Втома бере своє.
    
    Я провалююсь у сон...

    ...

    Перед очима оживають картини минулого. Такі яскраві, такі реальні.
    
    Ось я знову в класі, за своєю партою. Навколо гуде вулик голосів, по дошці скрипить крейда.
    
    Поруч - старі друзі. Ми шепочемось, жартуємо, намагаємось не розсміятися надто голосно.
    
    Раптом відчуваю на собі чийсь погляд. Підіймаю очі - і бачу Її.
    
    Вона посміхається. Тією самою посмішкою, що я пам'ятаю досі.
    
    Ми разом. Щасливі. Я нарешті знаходжу в собі сміливість сказати те, про що мріяв роками.
    
    "Я кохаю тебе." І чую у відповідь те саме.
    
    Неймовірне відчуття блаженства огортає мене. Я справді щасливий!

    Але... Щось не так. Здалеку долинають голоси.
    '''
    if not is_andriy_happy:
        $ caught_count += 1
    if not is_sasha_happy:
        $ caught_count += 1
    if not is_anna_happy:
        $ caught_count += 1
    mikko "Гарний сьогодні улов."
    if caught_count == 1:
        aila "Та вже хоч одного зловили. А то інші втекли, падли."
    elif caught_count == 2:
        aila "Та так. Двох зловили - вже не погано..."
    elif caught_count == 3:
        aila "Трьох зловили. Ще б одного і було б взагалі чудово."
    else:
        aila "І не кажи, аж чотирьох зловили. Давно такого не було."
    mikko "Це точно..."
    if caught_count >= 3:
        aila "Нам того, мабуть, до кінця зими вистачить!"
    else:
        aila "Схоже треба буде ще трохи попрацювати, щоб до кінця зими вистачило..."
    '''
    Що... що відбувається? Сон розсіюється, як туман на сонці. 

    Ні... ні! Я не хочу прокидатися! Благаю, дайте мені ще хвилинку з нею!

    Але реальність невблаганна. Це все обман. Пастка. І я... я потрапив у неї.

    Щось холодне і липке обволікає моє тіло. Не можу поворухнутися. Не можу дихати.

    Паніка накриває мене хвилею. Але... чому я не борюся? Чому не намагаюся втекти?

    Бо... я все ще відчуваю відлуння того щастя. Того моменту, коли вона посміхнулася мені.

    Може, це і є справжнє життя? Ці короткі миті радості серед океану болю?

    Темрява згущується. Думки уповільнюються. 

    Я відчуваю, як свідомість згасає. Це кінець. Але... я не боюся.

    Бо в останню мить я знову був щасливим. І, можливо... можливо, цього достатньо.
    '''
    window hide
    pause
    if persistent.bad_ending == False:
        $ renpy.notify("Кінцівку \"Порожній\" здобуто")
        $ persistent.bad_ending = True
    $ persistent.game_status = 1
    return

# Хороша кінцівка
label happy_end:
    i "Вибачте, але я все ж відмовлюсь."
    aila "Що? Ти не можеш відмовитись! Ти маєш залишитись тут, із нами!"
    i "Я кажу ні!"
    i "Я не хочу лишитись на вічно у минулому!"
    i "Я хочу жити далі!"
    "Я різко ривком кинувся до виходу, намагаючись якомога швидше втекти звідси."
    hide mikko
    hide aila
    "Але ноги наче налилися свинцем, кожен крок давався вкрай важко."
    "Тіло ніби розпухло від усієї їжі, яку я похапцем закидав в себе за столом. Але зупинятися я не міг!"
    mikko "Схопіть його!"
    "М'язи не слухалися, але я продовжував рухатися вперед, віддаляючись від переслідувачів."
    scene tunnel with fade
    "Нарешті я добіг до коридору і щодуху кинувся туди."
    scene underground with fade
    "Опинившись у каналізаційному відсіку, я миттєво схопився за драбину."
    "Туман внизу почав розсіюватися, і я якомога швидше поліз нагору, на поверхню."
    "Ледь чутно знизу долинув розчарований вигук Мікко:"
    mikko "От лихо, втік! Ну нічого, трьох нам вистачить."
    scene court with fade
    "Зрештою я вибрався на вулицю та щодуху припустив геть звідти."
    "Озирнувшись, я не побачив ні сліду ельфів, ні люка за собою."
    "Але від гріха подалі я вирішив не зупинятися, а якомога швидше віддалитися."
    scene black with eye_close
    scene court with eye_open
    "Нарешті я доставив ці прокляті документи в офіс! Думав, вони їх ніколи не приймуть."
    "Тепер я можу спокійно повертатися додому."
    "Але раз я вже тут, то пройдуся трохи знайомими місцями - до іншої зупинки."
    "Я минув двір, де колись ледь не потрапив у пастку. Тепер тут все спокійно."
    "Невдовзі попереду з'явилася автобусна зупинка. Я пришвидшив крок."
    phone "Привіт, сонечко, ти вже скоро будеш?"
    i "Так, вже майже на зупинці, кохана."
    phone "Чудово, чекаю! А, і не забудь купити цукерок для моїх батьків."
    i "Звісно, обов'язково куплю. Скоро побачимося!"
    "Я усміхнувся, згадавши нещодавні події. Якби я тоді погодився залишитися у минулому, то ніколи б не дізнався, яке прекрасне життя мене чекає."
    scene street with fade
    "Тому тепер я дивлюся лише вперед і живу сьогоденням."
    "З цими думками я сів у свій автобус, що віз мене назустріч майбутньому, а не минулому."
    window hide
    pause
    if persistent.happy_ending == False:
        $ renpy.notify("Кінцівку \"Новий шлях\" здобуто")
        $ persistent.happy_ending = True
    $ persistent.game_status = 2
    return